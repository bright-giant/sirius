import edu.sc.seis.launch4j.tasks.Launch4jLibraryTask

dependencies {
    implementation project(':sirius_gui')
    implementation group: 'org.slf4j', name: 'slf4j-jdk14', version: "$slf4j_version"
}


/*################### Environment variables ################### */
mainClassName = "de.unijena.bioinf.ms.frontend.SiriusGUIApplication"

launch4j{
    mainClassName = project.mainClassName
    jar = getLibraryDir() + "\\" + getRootProject().getChildProjects().get('sirius_gui').tasks.getByName('jar').outputs.files.singleFile.getName()
}

//additional GUI win launcher
task createGuiExe(type: Launch4jLibraryTask) {
    outfile = 'sirius-gui.exe'
    fileDescription = 'SIRIUS graphical user interface'
    headerType = "gui"
    cmdLine = "gui"
}

distImage {
    dependsOn getParent().tasks.getByName('extractRuntimeImageFX')
    mainClass = mainClassName
    imageName = 'sirius'
    runtimeImageDirectory = getParent().tasks.getByName('extractRuntimeImageFX').outputs.files.singleFile
    mainJarName = getRootProject().getChildProjects().get('sirius_gui').tasks.getByName('jar').outputs.files.singleFile.getName()
    winConsole = false
    macPackageName = "SIRIUS"
}

distInstaller {
    linuxShortcut = true
}

distribution.dependsOn 'signDistImage'
distribution.dependsOn 'signDistInstaller'
if (project.OS.isMacOsX())
    distribution.dependsOn 'macNotarize'
distribution.dependsOn 'createChecksums'
distribution.dependsOn 'distImageZip'
if (!OS.isLinux())
    distribution.dependsOn 'distInstaller'

publishing {
    publications {
        siriusMS(org.gradle.api.publish.maven.MavenPublication) {
            artifactId = "$name"
            groupId = "$group"
            artifact source: "${distImage.getImageDir().absolutePath}.zip", classifier: osName, extension: 'zip'
            if (!OS.isLinux())
                artifact source: "${OS.isMacOsX() ? distImage.getImageDir().absolutePath.replace('.app', installerAppendix) : distImage.getImageDir().absolutePath + installerAppendix}", classifier: osName, extension: installerNameType
        }
        sirius(org.gradle.api.publish.maven.MavenPublication) {
            artifactId = "$name"
            groupId = "$group"
            artifact source: "${distImage.getImageDir().absolutePath}.zip", classifier: osName, extension: 'zip'
            artifact source: "${distImage.getImageDir().absolutePath}.zip.sha256", classifier: osName, extension: 'zip.sha256'
            if (!OS.isLinux()){
                artifact source: "${OS.isMacOsX() ? distImage.getImageDir().absolutePath.replace('.app', installerAppendix) : distImage.getImageDir().absolutePath + installerAppendix}", classifier: osName, extension: installerNameType
                artifact source: "${OS.isMacOsX() ? distImage.getImageDir().absolutePath.replace('.app', installerAppendix) : distImage.getImageDir().absolutePath + installerAppendix}.sha256", classifier: osName, extension: "${installerNameType}.sha256"
            }
        }
    }
}
publishSiriusPublicationToMavenLocal.dependsOn 'distribution'
publishSiriusMSPublicationToMavenLocal.dependsOn 'distribution'
